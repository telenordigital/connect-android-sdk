apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

project.group = 'com.telenor.connect'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.2'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionName "3.0.0"
        buildConfigField('String' , 'VERSION_NAME', '"3.0.0"')
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable 'AppCompatCustomView'
        disable 'MissingPermission'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Cannot be updated due to minSdk 15
    api 'com.squareup.okhttp3:okhttp:3.11.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    api 'com.squareup.retrofit2:retrofit:2.4.0'
    api 'com.squareup.retrofit2:converter-gson:2.4.0'
    api 'androidx.browser:browser:1.0.0'
    // Cannot be update end
    api 'androidx.appcompat:appcompat:1.3.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'com.google.android.gms:play-services-auth:19.0.0'
    api 'com.google.android.gms:play-services-ads-identifier:17.0.1'
    api 'com.google.android.gms:play-services-base:17.6.0'
    api 'com.nimbusds:nimbus-jose-jwt:9.7'
    implementation 'commons-io:commons-io:2.6'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.telenor.connect'
                artifactId = 'final'
                version = '3.0.0'
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.telenor.connect'
                artifactId = 'final-debug'
                version = '3.0.0'
            }
        }
    }
}
